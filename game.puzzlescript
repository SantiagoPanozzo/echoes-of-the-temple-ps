title Crypt of the Necrotaker
author SantiagoPanozzo
homepage www.puzzlescript.net
verbose_logging
norepeat_action
noaction

========
OBJECTS
========

Background
 #191919


TargetLeft
gray white
00100
00010
11001
00010
00100

TargetRight
gray white
00100
01000
10011
01000
00100

TargetUp
gray white
00100
01010
10001
00100
00100

TargetDown
gray white
00100
00100
10001
01010
00100

Wall
#524b24 #5a532a #635a2c
00002
10000
00200
02000
00001

RedWall
#952b24 #9d332a #A63a2c
00002
10000
00200
02000
00001

RedPopupWall
transparent #9d332a #A63a2c
00002
10000
00200
02000
00001

BlueWall
#524b57 #5a535d #635a5f
00002
10000
00200
02000
00001

GreenWall
#527e24 #5a862a #638c2c
00002
10000
00200
02000
00001

DisabledFakeWall
transparent

EnablingFakeWall
transparent

Fireplace
#88411c #bd6637 #ffde00 #ff9b00 #ff0000 #cecdc8
5.4..
.4345
43234
32223
01010

FireplaceSwap
transparent

Fireplace2
#88411c #bd6637 #ffde00 #ff9b00 #ff0000 #cecdc8
..4.5
5434.
43234
32223
01010

FireplaceSwap2
transparent

Player
#435f48 #323c39 #fbf236 #8f563b #696a6a #45283c
0...0
.000.
.232.
41114
.5.5.

PlayerDead
#435f48 #323c39 #fbf236 #8f563b #696a6a #45283c
.....
..00.
.0000
00333
554.4

OtherDead
red #323c39 #fbf236 #8f563b #696a6a #45283c
.....
..00.
.0000
00333
554.4

SpikeActive
#696a6a #777777 #cacaca
.....
.....
.....
.2.2.
01010

SpikeDeactivating
#696a6a #777777 #cacaca
.....
.....
.....
.2.2.
01010

SpikeInactive
#696a6a #777777 #cacaca
.....
.....
.....
.....
01010

SpikeReactivating
#696a6a #777777 #cacaca
.....
.....
.....
.....
01010

RedSkull
#e6e6e6 #cecece #b9b9b9 #da3333 #ffffff
.004.
10104
31312
03102
.02..

BlueSkull
#e6e6e6 #cecece #b9b9b9 #639bff #ffffff
.004.
10104
31312
03102
.02..

GreenSkull
#e6e6e6 #cecece #b9b9b9 #339b33 #ffffff
.004.
10104
31312
03102
.02..

BreakingRedSkull
transparent

BreakingBlueSkull
transparent

BreakingGreenSkull
transparent

(Right)
REnemy
#5b6ee1 #da3333 #639bff #d2d2d2 #b9b9b9
.2.23
.2223
.1014
00000
.2.2.

(Left)
LEnemy
#5b6ee1 #da3333 #639bff #d2d2d2 #b9b9b9
32.2.
3222.
4101.
00000
.2.2.

(Up)
UEnemy
#ac3232 #da3333 #d95763 #d2d2d2 #b9b9b9
.2.23
.2223
.1014
00000
.2.2.

(Down)
DEnemy
#ac3232 #da3333 #d95763 #d2d2d2 #b9b9b9
32.2.
3222.
4101.
00000
.2.2.

AlreadyMoved
transparent


=======
LEGEND
=======

. = Background
# = Wall
? = RedWall
! = BlueWall
$ = GreenWall
P = Player
X = SpikeActive
O = SpikeInactive
á = TargetUp
é = TargetLeft
í = TargetRight
ó = TargetDown
R = RedSkull
B = BlueSkull
G = GreenSkull
F = Fireplace
u = UEnemy
h = REnemy
k = RedPopupWall

HorizontalEnemy = REnemy or LEnemy
VerticalEnemy = UEnemy or DEnemy

Enemy = horizontalenemy or verticalenemy

AnyTarget = TargetLeft or TargetRight or TargetUp or TargetDown
Killable = Player or HorizontalEnemy or VerticalEnemy
Killer = HorizontalEnemy or VerticalEnemy or SpikeActive
pusher = Player or enemy
Stoppable = Player
Solid = Wall or RedWall or BlueWall or GreenWall


=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
AlreadyMoved
anytarget, BreakingRedSkull, BreakingBlueSkull, BreakingGreenSkull
RedPopupWall
UEnemy
DEnemy
REnemy
LEnemy
OtherDead
SpikeActive, SpikeDeactivating, SpikeInactive, DisabledFakeWall, SpikeReactivating, Fireplace, Fireplace2, FireplaceSwap, FireplaceSwap2, EnablingFakeWall
Player, PlayerDead, Wall, RedSkull, BlueSkull, RedWall, BlueWall, GreenWall, GreenSkull

======
RULES
======

( Animations for fireplace, the swaps will never be instanced)
[ Fireplace ] -> [ FireplaceSwap ]
[ Fireplace2 ] -> [ FireplaceSwap2 ]

[ FireplaceSwap ] -> [ Fireplace2 ]
[ FireplaceSwap2 ] -> [ Fireplace ]

late [ BreakingRedSkull ] [ RedWall ] -> [ BreakingRedSkull ] [ ]
late [ BreakingRedSkull ] [ RedPopupWall ] -> [ BreakingRedSkull ] [ Wall ]
late [ BreakingBlueSkull ] [ BlueWall ] -> [ BreakingBlueSkull ] [ ]


[ SpikeActive ] -> [ SpikeDeactivating ]
[ SpikeInactive ] -> [ SpikeReactivating ]

[ SpikeDeactivating ] -> [ SpikeInactive ]
[ SpikeReactivating ] -> [ SpikeActive ]

[ > player | solid ] -> cancel

right [REnemy] -> [> REnemy]
left [LEnemy] -> [> LEnemy]
up [UEnemy] -> [> UEnemy]
down [DEnemy] -> [> DEnemy]

[> REnemy | enemy] -> [> REnemy | > REnemy]
[> DEnemy | enemy] -> [> DEnemy | > DEnemy]
[> LEnemy | enemy] -> [> LEnemy | > LEnemy]
[> UEnemy | enemy] -> [> UEnemy | > UEnemy]

[> REnemy | solid] -> [< LEnemy | solid]
[> LEnemy | solid] -> [< REnemy | solid]
[> UEnemy | solid] -> [< DEnemy | solid]
[> DEnemy | solid] -> [< UEnemy | solid]

[ > pusher | RedSkull ] -> [ pusher | > RedSkull ]
[ > RedSkull | Fireplace ] -> [ > BreakingRedSkull | ]
[ > RedSkull | Fireplace2 ] -> [ > BreakingRedSkull | ]


[ > pusher | BlueSkull ] -> [ pusher | > BlueSkull ]
[ > BlueSkull | Fireplace ] -> [ > BreakingBlueSkull | ]
[ > BlueSkull | Fireplace2 ] -> [ > BreakingBlueSkull | ]

late [ killer Player ] -> [ killer PlayerDead ]
late [ killer killable ] -> [ killer OtherDead ]

late [ Player SpikeActive ] -> [ PlayerDead SpikeActive ]
late [ Player VerticalEnemy ] -> [ PlayerDead VerticalEnemy ]
late [ Player HorizontalEnemy ] -> [ PlayerDead HorizontalEnemy ]
late [BreakingRedSkull] -> [ ]

==============
WINCONDITIONS
==============

no PlayerDead
no RedWall
no BlueWall
no GreenWall

all Player on anytarget

=======
LEVELS
=======
MESSAGE Clean up that cave! Destroy all those skulls!

############
############
#?????######
#?.f.?######
#?.r.?######
#?.p..oxox.é
#?...?######
#?????######
############
############

MESSAGE Ah, forgot about the spikes, don't worry, they react to your rythm

############
#..........é
#...########
###!!!....F#
########..B#
P...R..OF..#
######?##..#
######?....#
######?....#
############

MESSAGE Those goons just copy your rythm, they have no creativity!

############
P.....?...##
#.R..u?...##
#.F.####XX##
########..##
########..##
########.H##
########..##
########...é
############

Message How is this possible?

#######á####......
####.#k?..##......
###...kkkkk#.....#
#...fxh..........#
#..##.######.....#
#..u.ru.u..#.....#
#...u..u...#h..oho
#########..#..oho.
p.........##h..oho
############......
